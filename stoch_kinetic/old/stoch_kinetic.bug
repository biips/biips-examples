# Stochastic kinetic prey - predator model
# cf Boys, Wilkinson and Kirkwood
# Bayesian inference for a discretely observed stochastic kinetic model


var x_true[2,t_max], c_true[t_max], vect_true[4,t_max],
    x[2,t_max], c[t_max], vect[4,t_max], y[t_max]

data
{
  x_true[,1] <- x_init+0.0
  for (t in 2:t_max)
  { 
    vect_true[1, t] <- alpha_true*x_true[1, t-1]*dt
    vect_true[2, t] <- beta*x_true[1,t-1]*dt
    vect_true[3, t] <- gamma*x_true[2, t-1]*dt
    vect_true[4, t] <- 1.0-  alpha_true*x_true[1, t-1]*dt- beta*x_true[1,t-1]*dt-gamma*x_true[2, t-1]*dt
    c_true[t] ~ dcat(vect_true[,t])
    x_true[1, t] <- x_true[1, t-1] + (c_true[t]==1) - (c_true[t]==2)
    x_true[2, t] <- x_true[2, t-1] + (c_true[t]==2) - (c_true[t]==3)    
  }
  for (t in 1:t_max)	
  {
    y[t] ~ dnorm(x_true[1,t], prec_y) 
  }
}
model
{
  logalpha ~ dunif(-7,2)
  alpha <- exp(logalpha)
  x[,1] <- x_init+0.0
  for (t in 2:t_max)
  { 
    vect[1, t] <- alpha*x[1, t-1]*dt
    vect[2, t] <- beta*x[1,t-1]*dt
    vect[3, t] <- gamma*x[2, t-1]*dt
    vect[4, t] <- 1.0-  alpha*x[1, t-1]*dt- beta*x[1,t-1]*dt-gamma*x[2, t-1]*dt
    c[t] ~ dcat(vect[,t])
    x[1, t] <- x[1, t-1] + (c[t]==1) - (c[t]==2)
    x[2, t] <- x[2, t-1] + (c[t]==2) - (c[t]==3)    
  }
  for (t in 1:t_max)	
  {
    y[t] ~ dnorm(x[1,t], prec_y) 
  }
}
